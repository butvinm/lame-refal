*$FROM Canvas
$EXTERN SetFillStyle;
$EXTERN BeginPath;
$EXTERN Fill;
$EXTERN Arc;

*$FROM LibraryEx
$EXTERN Map;
$EXTERN Trim;

*$FROM FPF
$EXTERN FPF-ToInt;

*$FROM Interpolation
$EXTERN Linear;
$EXTERN Lagrange;

$ENTRY Go {
  =
    <Next
      /* (0) */
      (<JSFloatToFF <Arg 0>>)
      <ParsePoints <Arg 1>>
    >;
}

ParsePoints {
  e.Points = <Map (ParseRow) <Split '\n' <Trim e.Points>>>;
}

ParseRow {
  (e.Row) = (<Map JSFloatToFF-Bracketed <Split ';' e.Row>>);
}

Split {
  s.Del e.StrB s.Del e.StrE = (e.StrB) <Split s.Del e.StrE>;
  s.Del e.Str = (e.Str);
}

JSFloatToFF-Bracketed {
  (e.Float) = (<JSFloatToFF e.Float>);
}

JSFloatToFF {
  e.Int '.' s.F0 s.F1 s.F2 e.Rest = <Numb e.Int s.F0 s.F1 s.F2>;
  e.Int '.' s.F0 s.F1 = <Numb e.Int s.F0 s.F1 '0'>;
  e.Int '.' s.F0 = <Numb e.Int s.F0 '0' '0'>;
  e.Int = <Numb e.Int '0' '0' '0'>;
}


Next {
  (e.X) e.Points =
    <SetFillStyle 'rgb(0 0 0)'> <Map (DrawPoint 2) e.Points>
    <SetFillStyle 'rgb(255 0 0)'> <DrawPoint 1 ((e.X) (<Lagrange (e.X) e.Points>))>
    <SetFillStyle 'rgb(0 0 255)'> <DrawPoint 1 ((e.X) (<Linear (e.X) e.Points>))>;
}



ToCanvasCenterX { e.A = <Add (e.A) 500> }
ToCanvasCenterY { e.A = <Sub 500 e.A> }
CanvasScale { e.A = <Mul (e.A) 30> }

DrawPoint {
  s.R ((e.X) (e.Y)) =
    <BeginPath>
    <Arc
      (<ToCanvasCenterX <FPF-ToInt <CanvasScale e.X>>>)
      (<ToCanvasCenterY <FPF-ToInt <CanvasScale e.Y>>>)
      s.R 0 7 0
    >
    <Fill>;
}
